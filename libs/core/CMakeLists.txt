cmake_policy(SET CMP0144 NEW) # find_package uses upper-case _ROOT variables

include(CheckCXXSourceRuns)
include(CheckCXXSymbolExists)

project(monad_core LANGUAGES C CXX ASM)

option(CMAKE_EXPORT_COMPILE_COMMANDS ON)

option(MONAD_CORE_FORCE_DEBUG_ASSERT
        "Enable MONAD_DEBUG_ASSERT in any build mode" OFF)

# ##############################################################################
# deps
# ##############################################################################

include("cmake/find_our_dependency.cmake")

cmake_host_system_information(RESULT linux_flavour QUERY DISTRIB_ID)
if(linux_flavour MATCHES "debian|ubuntu")
  find_package(Boost REQUIRED COMPONENTS context fiber stacktrace_backtrace)
else()
  find_package(Boost REQUIRED COMPONENTS context fiber stacktrace_addr2line)
endif()
function(check_if_boost_fiber_needs_ucontext_macro)
  set(prog
      "#include <boost/context/fiber.hpp>\nint main(void){\n::boost::context::fiber foo;\nreturn 0;\n}"
  )
  set(CMAKE_REQUIRED_LIBRARIES "Boost::context")
  check_cxx_source_runs("${prog}" BOOST_FIBER_LINKS_WITHOUT_ADDITIONAL_MACROS)
  if(NOT BOOST_FIBER_LINKS_WITHOUT_ADDITIONAL_MACROS)
    set(CMAKE_REQUIRED_DEFINITIONS "-DBOOST_USE_UCONTEXT=1")
    check_cxx_source_runs("${prog}"
                          BOOST_FIBER_LINKS_WITH_BOOST_USE_UCONTEXT_MACRO)
    if(BOOST_FIBER_LINKS_WITH_BOOST_USE_UCONTEXT_MACRO)
      get_target_property(var Boost::context INTERFACE_COMPILE_DEFINITIONS)
      list(APPEND var "-DBOOST_USE_UCONTEXT=1")
      set_target_properties(Boost::context
                            PROPERTIES INTERFACE_COMPILE_DEFINITIONS "${var}")
    else()
      message(
        FATAL_ERROR
          "FATAL: Boost.Context needs additional help to link, and I need to be taught what"
      )
    endif()
  endif()
endfunction()
check_if_boost_fiber_needs_ucontext_macro()

find_package(TBB REQUIRED)

add_subdirectory("third_party/nanobench")

add_subdirectory("third_party/nlohmann_json" SYSTEM)

add_subdirectory("third_party/unordered_dense")

add_subdirectory("third_party/quill" SYSTEM)

add_library(
  openssl_sha3_asm INTERFACE
  "third_party/openssl/crypto/sha/asm/keccak1600-avx2.S"
  "third_party/openssl/crypto/sha/asm/keccak1600-avx512.S")
target_include_directories(openssl_sha3_asm INTERFACE "third_party/openssl")

# ##############################################################################
# libs
# ##############################################################################

# monad_core_c is a library containing source code that is compiled in C23 mode
# and contains no C++ source files; it may be linked by downstream libraries
# which only need C and do not want the C++ runtime as a dependency
add_library(monad_core_c OBJECT
  # core
  "src/monad/core/assert.c"
  "src/monad/core/assert.h"
  "src/monad/core/c_result.h"
  # util
  "src/monad/util/parse_util.h"
  "src/monad/util/parse_util.c")

if (MONAD_CORE_FORCE_DEBUG_ASSERT)
  target_compile_definitions(monad_core_c
          PUBLIC MONAD_CORE_FORCE_DEBUG_ASSERT=1)
endif()

target_compile_features(monad_core_c PUBLIC c_std_23)
target_compile_definitions(monad_core_c PUBLIC _GNU_SOURCE)
target_compile_options(monad_core_c PRIVATE -Wall -Werror -Wextra)
target_include_directories(monad_core_c
        PUBLIC "${PROJECT_SOURCE_DIR}/src")

add_library(
  monad_core STATIC
  # config
  "src/monad/config.hpp"
  # core
  "src/monad/core/array.hpp"
  "src/monad/core/backtrace.cpp"
  "src/monad/core/backtrace.hpp"
  "src/monad/core/basic_formatter.hpp"
  "src/monad/core/bit_util.h"
  "src/monad/core/boost_result.h"
  "src/monad/core/byte_string.hpp"
  "src/monad/core/bytes_hash_compare.hpp"
  "src/monad/core/cmemory.hpp"
  "src/monad/core/cleanup.h"
  "src/monad/core/cleanup.c"
  "src/monad/core/cpu_relax.h"
  "src/monad/core/c_result_impl.cpp"
  "src/monad/core/endian.hpp"
  "src/monad/core/hash.hpp"
  "src/monad/core/hex_literal.hpp"
  "src/monad/core/keccak.c"
  "src/monad/core/keccak.h"
  "src/monad/core/keccak.hpp"
  "src/monad/core/keccak_impl.S"
  "src/monad/core/likely.h"
  "src/monad/core/math.hpp"
  "src/monad/core/nibble.h"
  "src/monad/core/offset.hpp"
  "src/monad/core/result.hpp"
  "src/monad/core/size_of.hpp"
  "src/monad/core/small_prng.hpp"
  "src/monad/core/spinlock.h"
  "src/monad/core/thread.h"
  "src/monad/core/thread.c"
  "src/monad/core/unaligned.hpp"
  "src/monad/lru/lru_cache.hpp"
  "src/monad/lru/static_lru_cache.hpp"
  "src/monad/mem/batch_mem_pool.hpp"
  "src/monad/synchronization/spin_lock.hpp"
  # fiber
  "src/monad/fiber/config.hpp"
  "src/monad/fiber/priority_algorithm.cpp"
  "src/monad/fiber/priority_algorithm.hpp"
  "src/monad/fiber/priority_pool.cpp"
  "src/monad/fiber/priority_pool.hpp"
  "src/monad/fiber/priority_properties.hpp"
  "src/monad/fiber/priority_queue.cpp"
  "src/monad/fiber/priority_queue.hpp"
  "src/monad/fiber/priority_task.hpp"
  # io
  "src/monad/io/buffer_pool.cpp"
  "src/monad/io/buffer_pool.hpp"
  "src/monad/io/config.hpp"
  # mem
  "src/monad/mem/cpool.h"
  "src/monad/mem/dynamic_allocator.hpp"
  "src/monad/mem/huge_mem.h"
  "src/monad/mem/huge_mem.hpp"
  "src/monad/mem/mem_map.cpp"
  "src/monad/mem/mem_map.hpp"
  # rlp
  "src/monad/rlp/config.hpp"
  "src/monad/rlp/encode.hpp"
  # test
  "src/monad/test/gtest_signal_stacktrace_printer.hpp"
  # util
  "src/monad/util/parse_util_err.cpp"
  "src/monad/util/stopwatch.hpp")

if (${CMAKE_SYSTEM_NAME} MATCHES "Linux")
  # Append Linux-specific implementation files
  target_sources(monad_core PRIVATE
    # core
    "src/monad/core/cgroup.c"
    "src/monad/core/cgroup.h"
    "src/monad/core/cpuset.c"
    "src/monad/core/cpuset.h"
    # io
    "src/monad/io/buffers.cpp"
    "src/monad/io/buffers.hpp"
    "src/monad/io/ring.hpp"
    "src/monad/io/ring.cpp"
    # mem
    "src/monad/mem/huge_mem.c"
    "src/monad/mem/huge_mem.cpp"
    # procfs
    "src/monad/procfs/statm.c"
    "src/monad/procfs/statm.h")
endif()

target_include_directories(monad_core PUBLIC "src")
monad_compile_options(monad_core)

add_library(monad_core_stacktrace_options INTERFACE)
target_compile_definitions(monad_core_stacktrace_options
        INTERFACE "BOOST_STACKTRACE_LINK=1")
if(linux_flavour MATCHES "debian|ubuntu")
  target_compile_definitions(monad_core_stacktrace_options
          INTERFACE "BOOST_STACKTRACE_USE_BACKTRACE=1")
  target_link_libraries(monad_core_stacktrace_options
          INTERFACE Boost::stacktrace_backtrace)
else()
  target_compile_definitions(monad_core_stacktrace_options
          INTERFACE "BOOST_STACKTRACE_USE_ADDR2LINE=1")
  target_link_libraries(monad_core_stacktrace_options
          INTERFACE Boost::stacktrace_addr2line)
endif()
target_link_libraries(monad_core PUBLIC openssl_sha3_asm Boost::fiber TBB::tbb)
target_link_libraries(monad_core PUBLIC komihash)
target_link_libraries(monad_core PUBLIC ethash::keccak)
target_link_libraries(monad_core PUBLIC evmc)
target_link_libraries(monad_core PUBLIC monad_core_c)
target_link_libraries(monad_core PRIVATE monad_core_stacktrace_options)

if (${CMAKE_SYSTEM_NAME} MATCHES "Linux")
  target_link_libraries(monad_core PUBLIC cgroup uring)
endif()

add_library(
  monad_core_disas STATIC
  # core
  "src/monad/core/math_disas.cpp"
  "src/monad/core/offset_disas.cpp"
  "src/monad/core/spinlock_disas.c"
  "src/monad/mem/cpool_disas.c"
  # rlp
  "src/monad/rlp/encode_disas.cpp")
monad_compile_options(monad_core_disas)
target_link_libraries(monad_core_disas PUBLIC monad_core)

# Minimal "runtime" library needed to support pure C binaries. This contains
# functions that have C linkage and follow the C ABI but are implemented in
# C++. This is a dynamic library that statically links to the C++ runtime
# libraries and hides all symbols except those the `monad_` prefix, so it
# cannot relocate against any other dynamically loaded C++ runtime. All source
# files are recompiled rather than some other kind of reuse mechanism because
# the code generation must enable position independent code.
add_library(monad_core_crt SHARED
  # config
  "src/monad/config.hpp"
  # core
  "src/monad/core/backtrace.cpp"
  "src/monad/core/backtrace.hpp"
  "src/monad/core/c_result_impl.cpp"
  # util
  "src/monad/util/parse_util_err.cpp")
set_target_properties(monad_core_crt PROPERTIES
        CXX_VISIBILITY_PRESET hidden
        VISIBILITY_INLINES_HIDDEN ON)
target_include_directories(monad_core_crt PUBLIC "src")
monad_compile_options(monad_core_crt)
target_link_libraries(monad_core_crt PRIVATE monad_core_stacktrace_options)
if (NOT APPLE)
  if ("${CMAKE_CXX_FLAGS}" MATCHES ".*-fsanitize=.*" AND
      CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
    target_compile_options(monad_core_crt PUBLIC "-fno-sanitize=function,vptr")
  endif()
  target_link_options(monad_core_crt PRIVATE -static-libstdc++)
  target_link_options(monad_core_crt PRIVATE
          "-Wl,--version-script=${CMAKE_CURRENT_SOURCE_DIR}/src/monad/core/core_crt.sym")
endif()

# ##############################################################################
# tests
# ##############################################################################

enable_testing()

find_package(GTest REQUIRED)

add_subdirectory("test")

monad_add_test(bit_util_test src/monad/core/bit_util_test.cpp)
monad_add_test(huge_mem_test src/monad/mem/huge_mem_test.cpp)
monad_add_test(static_lru_test src/monad/lru/static_lru_test.cpp)
